apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.vivitale'

android {
	compileSdkVersion 28
	buildToolsVersion '28.0.3'
	defaultConfig {
		minSdkVersion 16
		targetSdkVersion 28
		versionCode 43
		versionName "0.0.43"

		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
		multiDexEnabled true
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}

		debug {
			//            applicationIdSuffix ".debug"
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}

		unsigned {
			//			versionNameSuffix '-unsigned'
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	lintOptions {
		checkReleaseBuilds false
		abortOnError false
	}
}

dependencies {
	api fileTree(dir: 'libs', include: ['*.jar'])
	// 已经整合进入BaseCore中的库
	api 'com.android.support:support-v4:28.0.0'
	api 'com.android.support:appcompat-v7:28.0.0'
	api 'com.android.support:design:28.0.0'
	api 'com.android.support:recyclerview-v7:28.0.0'
	api 'com.android.support:cardview-v7:28.0.0'
	api 'com.android.support:percent:28.0.0'
	api 'com.android.support.constraint:constraint-layout:1.1.3'
	// json相关
	api 'com.google.code.gson:gson:2.8.5'
	// Knife
	api 'com.jakewharton:butterknife:8.8.1'
	annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1' //Java 的butterknife注解处理器
	// RxJava
	api 'io.reactivex.rxjava2:rxjava:2.1.14'
	api 'io.reactivex.rxjava2:rxandroid:2.0.2'
	api 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
	api 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0' // 包含android库和基础库
	api 'com.trello.rxlifecycle2:rxlifecycle-kotlin:2.1.0' // 支持Kotlin语法的RxLifecycle基础库
	api 'com.trello.rxlifecycle2:rxlifecycle-android-lifecycle-kotlin:2.1.0' //支持Kotlin语法的Android库
	// EventBus
	api 'org.greenrobot:eventbus:3.1.1'
	//沉浸式
	api 'com.readystatesoftware.systembartint:systembartint:1.0.4'
	// 动画
	api 'com.nineoldandroids:library:2.4.0'
	api 'com.github.florent37:viewanimator:1.0.5'
	//腾讯MMKV 取代sp
	api 'com.tencent:mmkv:1.0.11'

	// 依据项目情况需自行整合的库
	// 网络请求
	compileOnly 'com.lzy.net:okgo:3.0.4' //可以单独使用，不需要依赖下方的扩展包
	compileOnly 'com.lzy.net:okrx2:2.0.2' //RxJava扩展支持，根据需要添加
	compileOnly 'com.lzy.net:okserver:2.0.5' //版本号使用 + 可以自动引用最新版
	compileOnly 'com.squareup.okhttp3:okhttp:3.8.1'
	compileOnly 'com.squareup.okio:okio:1.13.0'
	// 图片工具glide
	compileOnly 'com.github.bumptech.glide:glide:4.7.1'
	annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
	compileOnly 'com.github.bumptech.glide:okhttp3-integration:4.7.1'
	// RecyclerView适配器工具
	compileOnly 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.40'
	compileOnly 'cn.bingoogolapple:bga-baseadapter:1.2.9@aar'
	// 轮播图
	compileOnly 'cn.bingoogolapple:bga-banner:2.2.4@aar'
	// 二维码,一维码工具
	compileOnly 'cn.bingoogolapple:bga-qrcode-zxing:1.2.1'
	compileOnly 'cn.bingoogolapple:bga-qrcode-zbar:1.2.1'
	// 图片选择、预览、九宫格图片控件、拖拽排序九宫格图片控件,需要同时选择 bga-baseadapter
	compileOnly 'cn.bingoogolapple:bga-photopicker:1.2.8@aar'
	// 日历控件
	// compileOnly 'com.squareup:android-times-square:1.6.5@aar'
}

allprojects {
	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) {
			options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Xdiags:verbose"
		}
	}
}

// ---------------------- JitPack ----------------------
// 打包源码
task sourcesJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier = 'sources'
}

task javadoc(type: Javadoc) {
	failOnError  false
	source = android.sourceSets.main.java.sourceFiles
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
	classpath += configurations.compile
}

// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}
